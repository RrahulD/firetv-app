plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

apply from: '../version.gradle'

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "tv.accedo.xdk.dishtv"
        minSdkVersion 23
        targetSdkVersion 31
        // onstream 1.0 version code is *1013600*
        versionCode 1013600 + (versionMajor * 100 + (10 + versionMinor * 10) + versionPatch)
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        multiDexEnabled = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        firetv {
            keyAlias 'firetv'
            keyPassword '12345678'
            storeFile file('./keystore/firetv')
            storePassword '12345678'
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable true
        }

        release {
            minifyEnabled false
            signingConfig signingConfigs.firetv
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all {
        variant ->
            variant.outputs.each {
                output ->
                    def buildType = variant.buildType.name
                    def name = "dish_onstream_" + variant.versionName + "_" + buildType + ".apk"
                    output.outputFileName = name
            }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // local domain and data modules
    implementation project(":domain")
    implementation project(":data")

    // accedo shared libraries
    implementation project(":vdk-core")
    implementation project(":vdk-epg")

    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.2.1"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    implementation "androidx.webkit:webkit:1.4.0"
    implementation 'androidx.leanback:leanback:1.0.0'

    // koin
    // implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-androidx-scope:$koin_version"
    implementation "io.insert-koin:koin-androidx-viewmodel:$koin_version"

    // bitmovin player
    implementation 'com.bitmovin.player:player:3.10.0'

    // glide image loading library
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    // blur images
    implementation 'jp.wasabeef:blurry:4.0.0'

    // firebase
    implementation platform('com.google.firebase:firebase-bom:29.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
}